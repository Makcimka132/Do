/*
This Source Code Form is subject to the terms of the Mozilla
Public License, v. 2.0. If a copy of the MPL was not distributed
with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
*/

import <std/vector> <std/cstring> <std/ascii> <std/utf8>
import <std/thread> <std/strutil> <std/map> <std/hash>

namespace std {
    extern(private, linkname: "sscanf") int format(char* buff, char* fmt);

    (data: "data", length: "length") struct string {
        char* data;
        int length;
        int capacity;

        std::string this(char* str) {
            std::string this;
            if(str != null) {
                this.length = std::cstring::strlen(str);
                this.capacity = (this.length * 2) + 2;
                if(this.capacity <= 0) this.capacity = 2;
                this.data = std::malloc(this.capacity);

                int i = 0;
                while(i<this.length) {
                    this.data[i] = str[i];
                    i += 1;
                }
                this.data[i] = '\0';
            }
            else {
                this.length = 0;
                this.capacity = 2;
                this.data = std::malloc(this.capacity);
                this.data[0] = '\0';
            }
        } => this;

        std::string this(int n) {
            std::string this;
            this.length = 0;
            this.capacity = n + 1;
            if(this.capacity <= 0) this.capacity = 2;
            this.data = std::malloc(this.capacity);
            this.data[0] = '\0';
        } => this;

        void operator=(std::string* one, char* two) {one[0] = std::string(two);}

        void ~this {
            if(this.data != null) {
                std::free(this.data);
                this.data = null;
            }
        }

        void setValue(char* a) {
            if(a != null) {
                this.length = std::cstring::strlen(a);
                this.capacity = (this.length * 2) + 2;
                if(this.capacity <= 0) this.capacity = 2;
                if(this.data != null) std::free(this.data);
                this.data = std::malloc(this.capacity);

                int i = 0;
                while(i<this.length) {
                    this.data[i] = a[i];
                    i += 1;
                }
                this.data[i] = '\0';
            }
            else {
                this.length = 0;
                this.capacity = 2;
                if(this.data != null) std::free(this.data);
                this.data = std::malloc(this.capacity);
                this.data[0] = '\0';
            }
        }

        (inline) void set(int index, char ch) {
            @if(__RAVE_RUNTIME_CHECKS) {
                if((index >= 0) && (index < this.length)) this.data[index] = ch;
            };
            @if(!(__RAVE_RUNTIME_CHECKS)) {
                this.data[index] = ch;
            };
        }

        (inline) char at(int index) {
            @if(__RAVE_RUNTIME_CHECKS) {
                if((index >= 0) && (index < this.length)) return = this.data[index];
                else return = '\0';
            };
            @if(!(__RAVE_RUNTIME_CHECKS)) {
                return = this.data[index];
            };
        }

        (inline) bool isDeleted => (this.data == null);

        (inline) char* c => this.data;

        (inline) char toChar => this.data[0];
        (inline) long toLong => libc::strtol(this.c(), cast(char**)null, 10);
        (inline) int toInt => cast(int)this.toLong();

        int indexOf(char c) => std::cstring::indexOf(this.data, c);

        int indexOf(char* s) {
            char* ptr = std::cstring::strstr(this.data, s);
            if(ptr != null) return = ptoi(ptr) - ptoi(this.data);
            else return = -1;
        }

        (inline) int indexOf(std::string s) => this.indexOf(s.data);

        void add(char c) {
            if(this.data != null) {
                this.length += 1;
                if(this.length == this.capacity) {
                    this.data = std::realloc(this.data, this.capacity, this.capacity * 2);
                    this.capacity = this.capacity * 2;
                }
                this.data[this.length-1] = c;
            }
        }

        void append(std::string str) {
            if(this.data != null) {
                int i = 0;
                while(i < str.length) {
                    this.length += 1;
                    if(this.length == this.capacity) {
                        this.data = std::realloc(this.data, this.capacity, this.capacity * 2);
                        this.capacity = this.capacity * 2;
                    }
                    this.data[this.length-1] = str.data[i];
                    i += 1;
                }
            }
        }

        void appendC(char* cstr) {
            if(this.data != null) {
                int len = std::cstring::strlen(cstr);
                int i = 0;
                while(i < len) {
                    this.length += 1;
                    if(this.length == this.capacity) {
                        this.data = std::realloc(this.data, this.capacity, this.capacity * 2);
                        this.capacity = this.capacity * 2;
                    }
                    this.data[this.length-1] = cstr[i];
                    i += 1;
                }
            }
        }

        (inline) std::string copy {
            std::string cp = std::string("");
            cp.append(this);
        } => cp;

        std::string replace(char c, char to) {
            return = this.copy();

            int i = 0;
            while(i < this.length) {
                if(this.data[i] == c) return.data[i] = to;
                i += 1;
            }
        }

        std::string substring(int from, int to) {
            @if(__RAVE_RUNTIME_CHECKS) {
                if(((from >= 0) && (from < this.length)) && ((to >= 0) && (to < this.length))) {
                    return = "";
                    int i = from;

                    while(i < to) {
                        return.add(this.data[i]);
                        i += 1;
                    }
                }
            };
            @if(!(__RAVE_RUNTIME_CHECKS)) {
                return = "";
                int i = from;

                while(i < to) {
                    return.add(this.data[i]);
                    i += 1;
                }
            };
        }

        std::string operator+(std::string one, std::string two) {
            std::string third = "";
            for(int i=0; i<one.length; i+=1) third.add(one.data[i]);
            for(int i=0; i<two.length; i+=1) third.add(two.data[i]);
        } => third;

        bool operator==(std::string one, std::string two) {
            return = true;
            if(one.length != two.length) return = false;
            else {
                for(int i=0; i<one.length; i+=1) {
                    if(one.data[i] != two.data[i]) {
                        return = false;
                        break;
                    }
                }
            }
        }

        bool operator==(std::string one, char* two) {
            return = true;
            for(int i=0; i<one.length; i+=1) {
                if(one.data[i] != two[i]) {
                    return = false;
                    break;
                }
            }
        }

        std::vector<std::string> split(char ch) {
            return = std::vector<std::string>();

            int idx = 0;
            std::string buffer = "";
            while(idx < this.length) {
                if(this.data[idx] != ch) {
                    buffer.add(this.data[idx]);
                    idx += 1;
                }
                else {
                    if(buffer.length > 0) return.add(buffer.copy());
                    buffer.length = 0;
                    idx += 1;
                }
            }
            if(buffer.length > 0) return.add(buffer.copy());
            ~buffer;
        }

        std::string ltrim {
            int idx = 0;
            while((idx < this.length) && (std::ascii::isSpace(this.data[idx]))) idx += 1;
        } => this.substring(idx, this.length);

        std::string rtrim {
            int idx = this.length - 1;
            while(std::ascii::isSpace(this.data[idx])) idx -= 1;
        } => this.substring(0, idx + 1);

        (inline) std::string trim {
            int idxL = 0;
            int idxR = this.length - 1;
            while((idxL < this.length) && (std::ascii::isSpace(this.data[idxL]))) idxL += 1;
            while(std::ascii::isSpace(this.data[idxR])) idxR -= 1;
        } => this.substring(idxL, idxR + 1);

        std::string insert(int pos, char* str) {
            std::string buffer = "";
            int i = 0;
            while(i < pos) {
                buffer.add(this.data[i]);
                i += 1;
            }
            buffer.appendC(str);
            while(i < this.length) {
                buffer.add(this.data[i]);
                i += 1;
            }
        } => buffer;

        int hash => std::hash::crc32(this.data, this.length);
    }

    namespace string {
        (inline) std::string fromNumber(long n) {
            char[24] tempBuffer;
            std::cstring::ltos(n, &tempBuffer);
            std::string sNumber = std::string(&tempBuffer);
        } => sNumber;
        (inline) std::string fromNumber(int n) => std::string::fromNumber(cast(long)n);
        (inline) std::string fromNumber(short n) => std::string::fromNumber(cast(long)n);
        (inline) std::string fromNumber(char n) => std::string::fromNumber(cast(long)n);
        (inline) std::string fromNumber(bool n) => std::string::fromNumber(cast(long)n);

        (inline) std::string fromDoubleN(double n, int precision) {
            char[24] tempBuffer;
            std::cstring::dtos(n, precision, &tempBuffer);
            std::string sNumber = std::string(&tempBuffer);
        } => sNumber;
        (inline) std::string fromDouble(double n) => std::string::fromDoubleN(n, 8);

        (inline) std::string fromFloatN(float n, int precision) => std::string::fromDoubleN(cast(double)n, precision);
        (inline) std::string fromFloat(float n) => std::string::fromFloatN(n, 7);

        (inline) std::string fromUNumber(char n) => std::string::fromUNumber(cast(short)n);
    }

    (ctargs) void sprintBuffer {
        std::string* resultPtr = @getCurrArg(std::string*);
        std::string result = resultPtr[0];

        @skipArg();

        char[40] nBuffer;

        @foreachArgs() {
            @if(@tNequals(@getCurrArgType(), float)) {
                @if(@tNequals(@getCurrArgType(), double)) {
                    @if(@tNequals(@getCurrArgType(), std::string)) {
                        @if(@tNequals(@getCurrArgType(), char)) {
                            @if(@tEquals(@getCurrArgType(), bool)) {
                                result.appendC(std::cstring::fromBool(@getCurrArg(bool)));
                            };
                            @if(@tEquals(@getCurrArgType(), short) || @tEquals(@getCurrArgType(), int) || @tEquals(@getCurrArgType(), long)) {
                                nBuffer = [
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0'
                                ];
                                std::cstring::ltos(@getCurrArg(long), cast(char*)&nBuffer);
                                result.appendC(cast(char*)&nBuffer);
                            };
                            @if(@tEquals(@getCurrArgType(), cent)) {
                                nBuffer = [
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0'
                                ];
                                std::cstring::ctos(@getCurrArg(cent), cast(char*)&nBuffer);
                                result.appendC(cast(char*)&nBuffer);
                            };
                            @if(@tEquals(@getCurrArgType(), std::u32string)) {
                                std::u32string us = @getCurrArg(std::u32string);
                                for(int i=0; i<us.length; i+=1) {
                                    char[4] buf;
                                    int n = std::utf::encode(us.data[i], &buf[0]);
                                    if(n == 1) result.add(buf[0]);
                                    else if(n == 2) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                    }
                                    else if(n == 3) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                        result.add(buf[2]);
                                    }
                                    else if(n == 4) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                        result.add(buf[2]);
                                        result.add(buf[3]);
                                    }
                                }
                            };
                            @if(@tEquals(@getCurrArgType(), std::u8string)) {
                                std::u8string u8s = @getCurrArg(std::u8string);
                                if(u8s.data != null) {
                                    for(int i=0; i<u8s.bytes; i+=1) result.add(u8s.data[i]);
                                }
                                else result.appendC("null");
                            };
                            @if(@tEquals(@getCurrArgType(), int*)) {
                                uint* utf8Text = @getCurrArg(uint*);
                                for(int i=0; utf8Text[i] != '\0'; i+=1) {
                                    char[4] buf;
                                    int n = std::utf::encode(utf8Text[i], &buf[0]);
                                    if(n == 1) result.add(buf[0]);
                                    else if(n == 2) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                    }
                                    else if(n == 3) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                        result.add(buf[2]);
                                    }
                                    else if(n == 4) {
                                        result.add(buf[0]);
                                        result.add(buf[1]);
                                        result.add(buf[2]);
                                        result.add(buf[3]);
                                    }
                                }
                            };
                            @if(@tEquals(@getCurrArgType(), char*)) {
                                char* str = @getCurrArg(char*);
                                if(str != null) result.appendC(str);
                                else result.appendC("null");
                            };
                        };
                        @if(@tEquals(@getCurrArgType(), char)) {
                            result.add(@getCurrArg(char));
                        };
                    };
                    @if(@tEquals(@getCurrArgType(), std::string)) {
                        std::string s = @getCurrArg(std::string);
                        if(s.data != null) result.append(s);
                        else result.appendC("null");
                    };
                };
                @if(@tEquals(@getCurrArgType(), double)) {
                    nBuffer = [
                        '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                        '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                        '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                        '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                        '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0'
                    ];
                    std::cstring::dtos(@getCurrArg(double), 8, &nBuffer);
                    result.appendC(cast(char*)&nBuffer);
                };
            };
            @if(@tEquals(@getCurrArgType(), float)) {
                nBuffer = [
                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0',
                    '\0', '\0', '\0', '\0', '\0', '\0', '\0', '\0'
                ];
                std::cstring::dtos(@getCurrArg(double), 7, &nBuffer);
                result.appendC(cast(char*)&nBuffer);
            };
        };
        resultPtr[0] = result;
    }

    (volatile) int __sprintBufferSL = 0;
    std::string __sprintBuffer;
    bool sbInitialized = false;

    (ctargs) std::string gsprint {
        std::thread::spinlock::lock(&std::__sprintBufferSL);

        if(!std::sbInitialized) {
            std::__sprintBuffer = std::string(128);
            std::sbInitialized = true;
        }
        else std::__sprintBuffer.length = 0;

        @callWithBeforeArgs(std::sprintBuffer, &std::__sprintBuffer);

        std::thread::spinlock::unlock(&std::__sprintBufferSL);
    } => std::__sprintBuffer;

    (ctargs) std::string sprint {
        std::string result = "";
        @callWithBeforeArgs(std::sprintBuffer, &result);
    } => result;
}
